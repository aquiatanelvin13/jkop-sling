
/*
 * This file is part of Jkop
 * Copyright (c) 2016 Job and Esther Technologies, Inc.
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

class is StringObject:

func forCode(code as string) static as this
{
	return(new this().setCode(code))
}

func forMessage(message as string) static as this
{
	return(new this().setMessage(message))
}

func instance(code as string, message as string = null) static as this
{
	return(new this().setCode(code).setMessage(message))
}

func set(error as this, code as string,message as string = null) static as this
{
	if(error == null) {
		return(null)
	}
	error.setCode(code)
	error.setMessage(message)
	return(error)
}

func setErrorCode(error as this, code as string) static as this
{
	return(set(error, code, null))
}

func setErrorMessage(error as this,message as string) static as this
{
	return(set(error, null, message))
}

func isError(o as object) static as bool
{
	if(o == null) {
		return(false)
	}
	if(o is this == false) {
		return(false)
	}
	var e = o as this
	if(String.isEmpty(e.getCode()) && String.isEmpty(e.getMessage())) {
		return(false)
	}
	return(true)
}

func asString(error as this) static as string
{
	if(error == null) {
		return(null)
	}
	return(error.toString())
}

prop code as string
prop message as string

func clear as this
{
	code = null
	message = null
	return(this)
}

func toString() as string
{
	if(String.isEmpty(message) == false) {
		return(message)
	}
	if(String.isEmpty(code) == false) {
		return(code)
	}
	return(null)
}